// СнятьБлокировкуИБ
#Использовать "..//../../srс/Модули"

&Пластилин
Перем УправлятельКластера; // BSLLS:Typo-off

&Пластилин
Перем Настройки;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Если НЕ ТребуетВыполнения() Тогда
		Возврат Истина;
	КонецЕсли;

	Кластер = УправлятельКластера.Получить();
	
	Логирователь.ВЛог("Снятие блокировки информационной базы");

	ПараметрыКоманд = Настройки.ПараметрыКоманд;
	ПараметрыПодключения = Настройки.ПараметрыПодключения;

	ОставитьБлокировкуРеглЗаданий = НЕ Общий.ПризнакВключенияПараметра("СниматьБлокировкуРегламентныхЗаданий", 
																	ПараметрыКоманд);
	Попытка
		Кластер.СнятьБлокировкуИнформационнойБазы(ПараметрыПодключения.ИмяБазы, ОставитьБлокировкуРеглЗаданий);  
	Исключение
		Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
		Возврат Ложь;
	КонецПопытки;

	Логирователь.ВЛог("Блокировка информационной базы снята");

	Возврат Истина;

КонецФункции

&Асинх
Функция ВыполнитьКомандуАсинх(ПараметрыКоманды) Экспорт // BSLLS:Typo-off
	
	Возврат РаботаСКомандами.ПереопределитьВыполнитьКомандуАсинх(ПараметрыКоманды, ЭтотОбъект);

КонецФункции

Функция ТребуетВыполнения()
	
	Если Настройки.ИспользоватьДинамическоеОбновление
			ИЛИ Настройки.ОставитьБлокировкуИБ Тогда
		Возврат Ложь;	
	КонецЕсли;

	Возврат Истина;

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры