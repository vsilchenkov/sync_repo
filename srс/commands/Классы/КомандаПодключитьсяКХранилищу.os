// ПодключитьсяКХранилищу
#Использовать "..//../../srс/Модули"

&Пластилин
Перем Настройки;

&Пластилин
Перем УправлятельХранилища; // BSLLS:Typo-off
&Пластилин
Перем КомандаОтключитьКонфигураторОтИнформационнойБазы;
&Пластилин
Перем КомандаЗахватитьОбъектыВХранилище;
&Пластилин
Перем КомандаОтменитьЗахватОбъектовВХранилище;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off

	Если Не КомандаОтключитьКонфигураторОтИнформационнойБазы.ВыполнитьКоманду() Тогда
		Возврат Ложь;	
	КонецЕсли;

	Параметры = ПараметрыКоманды.Параметры;
	ИмяРасширения = Параметры.Получить("ИмяРасширения");
	Если Не ЗначениеЗаполнено(ИмяРасширения) Тогда
		ИмяРасширения = "";		
	КонецЕсли;

	Если ЗначениеЗаполнено(ИмяРасширения) Тогда

		ТекстДополнения = ". Расширение - " + ИмяРасширения;

	Иначе

		ТекстДополнения = "";

		// Проверка что база уже поключена, так быстрее.
		Отказ = Ложь;
		Подключено = ПодключеноКХранилищу(Отказ);
		Если Отказ Тогда
			Возврат Ложь;		
		КонецЕсли;

		Если Подключено Тогда
			Логирователь.ВЛог("База к хранилищу подключена");
			Возврат Истина;		
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстСообщения = "Подключение к хранилищу";
	Логирователь.ВЛог(ТекстСообщения + ТекстДополнения);

	Хранилище = УправлятельХранилища.Получить(ИмяРасширения);
	
	Попытка
		ИгнорироватьНаличиеПодключеннойБД  = Истина;
		ЗаменитьКонфигурациюБД = Истина;
		Хранилище.ПодключитьсяКХранилищу(ИгнорироватьНаличиеПодключеннойБД, ЗаменитьКонфигурациюБД);
	Исключение
		Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
		Возврат Ложь;
	КонецПопытки;

	ТекстСообщения = "Подключение к хранилищу выполнено";
	Логирователь.ВЛог(ТекстСообщения + ТекстДополнения);

	Возврат Истина;

КонецФункции

Функция ПодключеноКХранилищу(Отказ = Ложь)

	ФайлыНастроек = Настройки.ФайлыНастроек;
	
	Логирователь.ВЛог("Проверяю подключение базы к хранилищу");	
	
	ПоФайлу = Истина;
	ИмяФайлаОбъектов = ФайлыНастроек.repo_test;
	
	Подключено = Истина;

	Попытка
		КомандаЗахватитьОбъектыВХранилище.ВыполнитьКомандуСПараметрами(ПоФайлу, ИмяФайлаОбъектов, Истина);
		КомандаОтменитьЗахватОбъектовВХранилище.ВыполнитьКомандуСПараметрами(ПоФайлу, ИмяФайлаОбъектов, Истина); 
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Если СтрНайти(ОписаниеОшибки, "Ошибка захвата объектов в хранилище") > 0 Тогда
			Подключено = Ложь;			
		Иначе
			Логирователь.ВЛог(ОписаниеОшибки, Истина, Истина);
			Отказ = Истина;
			Возврат Ложь;
		КонецЕсли;
	КонецПопытки;

	Возврат Подключено;

КонецФункции

&Асинх
Функция ВыполнитьКомандуАсинх(ПараметрыКоманды) Экспорт // BSLLS:Typo-off
	
	Возврат РаботаСКомандами.ПереопределитьВыполнитьКомандуАсинх(ПараметрыКоманды, ЭтотОбъект);

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры