// ЗадатьВопрос
#Использовать gui
#Использовать "..//../../srс/Модули"

&Пластилин
Перем Настройки;

&Пластилин
Перем Завершатель;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Результат = Истина;
	Если Настройки.Фоново Тогда
		Возврат Результат;		
	КонецЕсли;
	
	ПараметрыКоманд = Настройки.ПараметрыКоманд;
	ПараметрыОбщие  = Настройки.ПараметрыОбщие;

	ТекстВопроса = ПараметрыКоманды.Параметры.Получить("ТекстВопроса");
	Если ТекстВопроса = Неопределено Тогда
		ТекстВопроса = ПараметрыКоманд.ТекстВопроса;	
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекстВопроса) Тогда
		ТекстКоманд = ПараметрыОбщие.Получить("ПутьКФайлуCommands");
		Если Не ЗначениеЗаполнено(ТекстКоманд) Тогда
			ТекстКоманд = ПараметрыОбщие.Получить("ИдентификаторКоманд");			
		КонецЕсли;		
		ТекстВопроса = "Запустить " + ТекстКоманд + "?";		
	КонецЕсли;

	Логирователь.ЗаменитьПараметрыВТексте(ТекстВопроса);
	Логирователь.ДополнитьОсновныеПараметрыВТексте(ТекстВопроса);

	УправляемыйИнтерфейс = Новый УправляемыйИнтерфейс();
	Диалоги = УправляемыйИнтерфейс.СтандартныеДиалоги;
	Ответ = Диалоги.Вопрос(ТекстВопроса, 
							РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет , "sync_repo", КодВозвратаДиалога.Нет);
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Завершатель.ЗавершитьПоОшибке();
	КонецЕсли;

	Возврат Истина;

КонецФункции

&Асинх
Функция ВыполнитьКомандуАсинх(ПараметрыКоманды) Экспорт // BSLLS:Typo-off
	
	Возврат РаботаСКомандами.ПереопределитьВыполнитьКомандуАсинх(ПараметрыКоманды, ЭтотОбъект);

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры