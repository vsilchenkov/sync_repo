// ПоказатьПриветствие
#Использовать "..//../../srс/Модули"

&Пластилин
Перем Настройки;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

&Пластилин
Перем НаборЭмодзи;

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	ТекстПриветствия = ПараметрыКоманды.Параметры.Получить("Приветствие");
	Если ТекстПриветствия = Неопределено Тогда
		ТекстПриветствия = Настройки.ПараметрыКоманд.Приветствие;	
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ТекстПриветствия) Тогда
		Логирователь.ВЛог("Не заполнен текст приветствия");
		Возврат Ложь;		
	КонецЕсли;	

	Если Общий.ПризнакВключенияПараметра("УстановитьПаузу", Настройки.КомандыВыполнения) Тогда
		ПаузаПередЗапуском = Настройки.ПаузаПередЗапуском;
		Если ЗначениеЗаполнено(ПаузаПередЗапуском) Тогда
			стрСекундПаузы = Строка(ПаузаПередЗапуском);	
		Иначе
			стрСекундПаузы = Настройки.ПараметрыКоманд.Пауза;	
		КонецЕсли;		
	Иначе
		стрСекундПаузы = "";
	КонецЕсли;

	Если СтрНайти(ТекстПриветствия, "Пауза") > 0 
			И ЗначениеЗаполнено(стрСекундПаузы) Тогда		
		
		СекундПаузы = Число(стрСекундПаузы);
		
		Минута = 60;
		МинутПаузы = Цел(СекундПаузы / Минута);

		Если МинутПаузы > 0 Тогда
			СтрокаПаузы = Строка(МинутПаузы) + " мин";
		ИначеЕсли СекундПаузы > 0 Тогда
			СтрокаПаузы = Строка(СекундПаузы) + " сек";
		Иначе
			СтрокаПаузы = "";
		КонецЕсли; 
		
		ТекстПриветствия = Логирователь.ЗаменитьПараметрВТексте(ТекстПриветствия, "Пауза", СтрокаПаузы);

	Иначе
		
		ТекстПриветствия = Логирователь.ЗаменитьПараметрВТексте(ТекстПриветствия, " через #Пауза", "", "");

	КонецЕсли;
	
	Логирователь.ВЛог(ТекстПриветствия, , Истина, , НаборЭмодзи.Инфо());	

	Возврат Истина;

КонецФункции

&Асинх
Функция ВыполнитьКомандуАсинх(ПараметрыКоманды) Экспорт // BSLLS:Typo-off
	
	Возврат РаботаСКомандами.ПереопределитьВыполнитьКомандуАсинх(ПараметрыКоманды, ЭтотОбъект);

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()
КонецПроцедуры