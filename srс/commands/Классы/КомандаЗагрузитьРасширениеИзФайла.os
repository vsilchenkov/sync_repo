// ЗагрузитьРасширениеИзФайла
#Использовать "..//../../srс/Модули"

&Пластилин
Перем Настройки;

&Пластилин
Перем УправлятельКонфигуратора; // BSLLS:Typo-off
&Пластилин
Перем КомандаОтключитьКонфигураторОтИнформационнойБазы;
&Пластилин

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Если Не КомандаОтключитьКонфигураторОтИнформационнойБазы.ВыполнитьКоманду() Тогда
		Возврат Ложь;	
	КонецЕсли;

	Параметры = ПараметрыКоманды.Параметры;
	ИмяРасширения = Параметры.Получить("ИмяРасширения");
	ИмяФайла = Параметры.Получить("ИмяФайла");
	
	ОбновитьКонфигурациюИБ = Параметры.Получить("ОбновитьКонфигурациюИБ");
	Если Не ТипЗнч(ОбновитьКонфигурациюИБ) = Тип("Булево") Тогда
		ОбновитьКонфигурациюИБ = Ложь;			
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ИмяФайла) Или Не ЗначениеЗаполнено(ИмяРасширения) Тогда
		ТекстСообщения = "Не могу выполнить команду ""ЗагрузитьРасширениеИзФайла""" 
							+ " - не переданы параметры ""ИмяФайла"" или ""ИмяРасширения""";
		Логирователь.ВЛог(ТекстСообщения, Истина);
		Возврат Ложь;	
	КонецЕсли;
	
	Конфигуратор = УправлятельКонфигуратора.Получить();
	
	Логирователь.ВЛог("Загружаю расширение " + ИмяРасширения + " из файла " + ИмяФайла);

	ПутьККаталогуCFe = Настройки.ПутьККаталогуCFe;
	ПутьКФайлу = ОбъединитьПути(ПутьККаталогуCFe, ИмяФайла);

	Если Не РаботаСФайлами.ФайлСуществует(ПутьКФайлу) Тогда
		Логирователь.ВЛог("Файл расширения на найден - " + ПутьКФайлу);
		Возврат Ложь;		
	КонецЕсли;

	Попытка
		Конфигуратор.ЗагрузитьРасширениеИзФайла(ПутьКФайлу, ИмяРасширения, ОбновитьКонфигурациюИБ);
	Исключение
		Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
		Возврат Ложь;
	КонецПопытки;

	Логирователь.ВЛог("Расширение " + ИмяРасширения + " из файла " + ИмяФайла + " загружено");

	Возврат Истина;

КонецФункции

&Асинх
Функция ВыполнитьКомандуАсинх(ПараметрыКоманды) Экспорт // BSLLS:Typo-off
	
	Возврат РаботаСКомандами.ПереопределитьВыполнитьКомандуАсинх(ПараметрыКоманды, ЭтотОбъект);

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры