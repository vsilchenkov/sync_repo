
Функция СвойствоСтруктуры(пСтруктура, Ключ, ЗначениеПоУмолчанию = Неопределено) Экспорт

	Если ТипЗнч(пСтруктура) <> Тип("Структура") Тогда
		Возврат ЗначениеПоУмолчанию;		
	КонецЕсли;

	Результат = ЗначениеПоУмолчанию;
	Если пСтруктура.Свойство(Ключ, Результат) Тогда
		Возврат Результат;
	Иначе
		Возврат ЗначениеПоУмолчанию;
	КонецЕсли;
	
КонецФункции

Процедура ОбъединитьСоответствия(Приемник, Источник) Экспорт
	
	Для каждого КлючЗначение Из Источник Цикл
		Приемник.Вставить(КлючЗначение.Ключ, КлючЗначение.Значение);	
	КонецЦикла;

КонецПроцедуры

Функция ПризнакВключенияПараметра(ИмяПарамета, ДанныеПараметров) Экспорт
	
	Если ТипЗнч(ДанныеПараметров) = Тип("Структура") Тогда
		Признак = ДанныеПараметров[ИмяПарамета];
	ИначеЕсли ТипЗнч(ДанныеПараметров) = Тип("Массив") Тогда
		Признак = ДанныеПараметров.Найти(ИмяПарамета) <> Неопределено;
	Иначе
		Признак = ДанныеПараметров.Получить(ИмяПарамета);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Признак) Тогда
		Возврат Ложь; 
	КонецЕсли;
	
	Возврат Булево(Признак);

КонецФункции

#Область Копирование_коллекций

Функция СкопироватьРекурсивно(Источник) Экспорт
	
	Перем Приемник;
	
	ТипИсточника = ТипЗнч(Источник);
	Если ТипИсточника = Тип("Структура") Тогда
		Приемник = СкопироватьСтруктуру(Источник);
	ИначеЕсли ТипИсточника = Тип("Соответствие") Тогда
		Приемник = СкопироватьСоответствие(Источник);
	ИначеЕсли ТипИсточника = Тип("Массив") Тогда
		Приемник = СкопироватьМассив(Источник);	
	Иначе
		Приемник = Источник;
	КонецЕсли;
	
	Возврат Приемник;
	
КонецФункции

Функция СкопироватьСтруктуру(СтруктураИсточник) Экспорт
	
	СтруктураРезультат = Новый Структура;
	
	Для Каждого КлючИЗначение Из СтруктураИсточник Цикл
		СтруктураРезультат.Вставить(КлючИЗначение.Ключ, СкопироватьРекурсивно(КлючИЗначение.Значение));
	КонецЦикла;
	
	Возврат СтруктураРезультат;
	
КонецФункции

Функция СкопироватьСоответствие(СоответствиеИсточник) Экспорт
	
	СоответствиеРезультат = Новый Соответствие;
	
	Для Каждого КлючИЗначение Из СоответствиеИсточник Цикл
		СоответствиеРезультат.Вставить(КлючИЗначение.Ключ, СкопироватьРекурсивно(КлючИЗначение.Значение));
	КонецЦикла;
	
	Возврат СоответствиеРезультат;

КонецФункции

Функция СкопироватьМассив(МассивИсточник) Экспорт
	
	МассивРезультат = Новый Массив;
	
	Для Каждого Элемент Из МассивИсточник Цикл
		МассивРезультат.Добавить(СкопироватьРекурсивно(Элемент));
	КонецЦикла;
	
	Возврат МассивРезультат;
	
КонецФункции

#КонецОбласти