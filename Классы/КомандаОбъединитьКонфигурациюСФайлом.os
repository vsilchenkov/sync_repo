// ОбъединитьКонфигурациюСФайлом
#Использовать "../srс/Модули"

&Пластилин
Перем Настройки;

&Пластилин
Перем УправлятельКонфигуратора;
&Пластилин
Перем КомандаОтключитьКонфигураторОтИнформационнойБазы;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Если Не КомандаОтключитьКонфигураторОтИнформационнойБазы.ВыполнитьКоманду() Тогда
		Возврат Ложь;	
	КонецЕсли;

	Конфигуратор = УправлятельКонфигуратора.Получить();
	
	Логирователь.ВЛог("Начинаю объединение конфигурации с файлом");

	ФайлыНастроек = Настройки.ФайлыНастроек;
	ПараметрыПодключения = Настройки.ПараметрыПодключения;

	ФайлКонфигурации = ФайлыНастроек.ПутьККаталогуCF 
						+ "\" 
						+ РаботаСФайлами.СформироватьИмяФайла(ПараметрыПодключения.Идентификатор, "cf");

	Если НЕ РаботаСФайлами.ФайлСуществует(ФайлКонфигурации) Тогда
		Логирователь.ВЛог("Не найден CF файл - " + ФайлКонфигурации, Истина);
		Возврат Ложь;		
	КонецЕсли;

	ФайлMerge = Настройки.ПолныйПутьКФайлуНастроек(ФайлыНастроек.merge);

	Попытка
		Конфигуратор.ОбъединитьКонфигурациюСФайлом(ФайлКонфигурации, ФайлMerge);
	Исключение
		Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
		Возврат Ложь;
	КонецПопытки;

	Логирователь.ВЛог("Объединение конфигурации с файлом выполнено");

	Возврат Истина;

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры