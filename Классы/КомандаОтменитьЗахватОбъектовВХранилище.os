// ОтменитьЗахватОбъектовВХранилище
&Пластилин
Перем Настройки;

&Пластилин
Перем УправлятельКонфигуратора;
&Пластилин
Перем КомандаОтключитьКонфигураторОтИнформационнойБазы;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Возврат ВыполнитьКомандуСПараметрами(, , , ПараметрыКоманды);

КонецФункции

Функция ВыполнитьКомандуСПараметрами(ПоФайлу = Ложь, ИмяФайла = "", 
										ПроверкаПодключения = Ложь, ПараметрыКоманды = Неопределено) Экспорт // BSLLS:UnusedParameters-off
	
	Если Не КомандаОтключитьКонфигураторОтИнформационнойБазы.ВыполнитьКоманду() Тогда
		Возврат Ложь;	
	КонецЕсли;

	Если НЕ ПоФайлу Тогда
		Логирователь.ВЛог("Отменяю захват объектов в хранилище");
	Иначе 
		Логирователь.ВЛог("Отменяю объекты в хранилище по файлу " + ИмяФайла);
	КонецЕсли;

	ИгнорироватьИзменения = Истина;

	Если ПоФайлу Тогда
		ПутьКФайлуСоСпискомОбъектов = Настройки.ПолныйПутьКФайлуНастроек(ИмяФайла);
	Иначе
		ПутьКФайлуСоСпискомОбъектов = "";
	КонецЕсли;

	Конфигуратор = УправлятельКонфигуратора.Получить();
	ПараметрыПодключения = Настройки.ПараметрыПодключения;

	Попытка
		Конфигуратор.ОтменитьЗахватОбъектовВХранилище(ПараметрыПодключения.ПутьКХранилищу, 
												ПараметрыПодключения.ЛогинКХранилищу, 
												ПараметрыПодключения.ПарольКХранилищу, 
												ПутьКФайлуСоСпискомОбъектов,
												ИгнорироватьИзменения);
	Исключение
		Если ПроверкаПодключения Тогда
			ВызватьИсключение ОписаниеОшибки();
		Иначе
			Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
			Возврат Ложь;
		КонецЕсли;		
	КонецПопытки;

	Если НЕ ПоФайлу Тогда
		Логирователь.ВЛог("Отмена захвата объектов в хранилище выполнена");
	Иначе
		Логирователь.ВЛог("Отмена захвата объектов в хранилище по файлу " + ИмяФайла + " выполнена");		
	КонецЕсли;

	Возврат Истина;

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры