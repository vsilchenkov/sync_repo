// ПолучитьВерсиюКонфигурацииИзХранилища
#Использовать "../srс/Модули"

&Пластилин
Перем Настройки;

&Пластилин
Перем УправлятельКонфигуратора;
&Пластилин
Перем КомандаОтключитьКонфигураторОтИнформационнойБазы;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Если Не КомандаОтключитьКонфигураторОтИнформационнойБазы.ВыполнитьКоманду() Тогда
		Возврат Ложь;	
	КонецЕсли;

	Конфигуратор = УправлятельКонфигуратора.Получить();
	
	Логирователь.ВЛог("Получение версии конфигурации из хранилища");

	ПараметрыПодключения = Настройки.ПараметрыПодключения;
	НомерВерсии = Неопределено;
	
	Попытка
		ФайлИсходный = Конфигуратор.ПолучитьВерсиюИзХранилища(ПараметрыПодключения.ПутьКХранилищу, 
												ПараметрыПодключения.ЛогинКХранилищу, 
												ПараметрыПодключения.ПарольКХранилищу, 
												НомерВерсии);
	Исключение
		Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
		Возврат Ложь;
	КонецПопытки;

	ФайлыНастроек = Настройки.ФайлыНастроек;

	ФайлКонечный = ФайлыНастроек.ПутьККаталогуCF
								+ "\" 
								+ РаботаСФайлами.СформироватьИмяФайла(ПараметрыПодключения.Идентификатор, "cf");
									
	Если РаботаСФайлами.ФайлСуществует(ФайлКонечный) Тогда
		УдалитьФайлы(ФайлКонечный);		
	КонецЕсли;

	ПереместитьФайл(ФайлИсходный, ФайлКонечный);

	Логирователь.ВЛог("Версия конфигурации из хранилища сохранена");

	Возврат Истина;

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры