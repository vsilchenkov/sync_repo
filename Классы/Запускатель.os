&Пластилин
Перем ЗаполнительНастроек;

&Пластилин
Перем Настройки;

&Пластилин
Перем Выполнитель; // BSLLS:Typo-off

&Пластилин
Перем Логирователь;  // BSLLS:Typo-off

&Пластилин
Перем Завершатель; // BSLLS:Typo-off

&Пластилин
Перем КомандыПоКлючу;

&Пластилин("Команда")
Перем КомандаПоУмолчанию;

Процедура ПриЗапускеПриложения() Экспорт

	ЗаполнительНастроек.ЗаполнитьНачальныеНастройки();
	
	Если Настройки.ГрупповыеКоманды.Количество() > 0 Тогда
		ВыполнитьГрупповыеКоманды();
	Иначе
		ВыполнитьКоманды();
	КонецЕсли;
	
	Завершатель.Завершить();

КонецПроцедуры

Процедура ВыполнитьКоманды(Позиция = 1) // BSLLS:CognitiveComplexity-off
	
	КомандыВыполнения = Настройки.КомандыВыполнения;
	КоличествоКоманд = КомандыВыполнения.Количество();

	ПараметрыКоманды = Новый Структура("Ключ, Позиция", "", 0);

	Пока Позиция <= КоличествоКоманд Цикл
		
		КлючКоманды = КомандыВыполнения[Позиция - 1];
		
		ПараметрыКоманды.Ключ    = КлючКоманды;
		ПараметрыКоманды.Позиция = Позиция;

		Команда = КомандыПоКлючу[ВРег(КлючКоманды)];
		Если Команда = Неопределено Тогда
			Команда = КомандаПоУмолчанию;
		КонецЕсли;
		
		Попытка
			Результат = Команда.ВыполнитьКоманду(ПараметрыКоманды); 	
		Исключение
			Логирователь.ВЛог(ОписаниеОшибки(), Истина);
			Результат = Ложь;
		КонецПопытки;	

		Если Результат = Ложь Тогда
			
			Если Настройки.РежимDebug = Истина Или Настройки.ПозицияВГрупповой > 0 Тогда
				Завершатель.ЗавершитьПоОшибке();				
			КонецЕсли;

			СтрокаВвода = "";
			Подсказка = "Для продолжения набери команду"
							+ Символы.ПС
							+ "1 - запустить повторно текущий этап"
							+ Символы.ПС
							+ "2 - запустить сначала сценарий " + Настройки.ИдентификаторБазы
							+ Символы.ПС
							+ "0 - выйти"
							+ Символы.ПС;
							
			Пока ВвестиСтроку(СтрокаВвода, Подсказка, 1, Ложь) Цикл
				
				Если СтрокаВвода = "1" Тогда
					ВыполнитьКоманды(Позиция);
				ИначеЕсли СтрокаВвода = "2" Тогда
					ВыполнитьКоманды(1);
				ИначеЕсли СтрокаВвода = "0" Тогда
					Завершатель.ЗавершитьПоОшибке();
				Иначе
					Сообщить("Введено неизвестное значение");
					СтрокаВвода = "";
					Продолжить;
				КонецЕсли;

			КонецЦикла;
			
		КонецЕсли;

		Позиция = Позиция + 1;

	КонецЦикла;

КонецПроцедуры

Процедура ВыполнитьГрупповыеКоманды()
	Выполнитель.ВыполнитьКомандыМногопоточно(Настройки.ГрупповыеКоманды);	
КонецПроцедуры

&Рогатка
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры