// ПодключитьсяКХранилищу
&Пластилин
Перем Настройки;

&Пластилин
Перем УправлятельКонфигуратора;
&Пластилин
Перем КомандаОтключитьКонфигураторОтИнформационнойБазы;
&Пластилин
Перем КомандаЗахватитьОбъектыВХранилище;
&Пластилин
Перем КомандаОтменитьЗахватОбъектовВХранилище;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Если Не КомандаОтключитьКонфигураторОтИнформационнойБазы.ВыполнитьКоманду() Тогда
		Возврат Ложь;	
	КонецЕсли;

	Конфигуратор = УправлятельКонфигуратора.Получить();
	
	ФайлыНастроек = Настройки.ФайлыНастроек;
	
	Логирователь.ВЛог("Проверяю подключение базы к хранилищу");	
	
	ПоФайлу = Истина;
	ИмяФайлаОбъектов = ФайлыНастроек.repo_test;
	Подключено = Истина;
	Попытка
		КомандаЗахватитьОбъектыВХранилище.ВыполнитьКомандуСПараметрами(ПоФайлу, ИмяФайлаОбъектов, Истина);
		КомандаОтменитьЗахватОбъектовВХранилище.ВыполнитьКомандуСПараметрами(ПоФайлу, ИмяФайлаОбъектов, Истина); 
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Если СтрНайти(ОписаниеОшибки, "Ошибка захвата объектов в хранилище") > 0 Тогда
			Подключено = Ложь;			
		Иначе
			Логирователь.ВЛог(ОписаниеОшибки, Истина, Истина);
			Возврат Ложь;
		КонецЕсли;
	КонецПопытки;
	
	Если Подключено Тогда
		Логирователь.ВЛог("База к хранилищу подключена");
		Возврат Истина;		
	КонецЕсли;

	Логирователь.ВЛог("Подключение к хранилищу");

	ПараметрыПодключения = Настройки.ПараметрыПодключения;
	
	Попытка
		ИгнорироватьНаличиеПодключеннойБД = Истина;
		Конфигуратор.ПодключитьсяКХранилищу(ПараметрыПодключения.ПутьКХранилищу, 
												ПараметрыПодключения.ЛогинКХранилищу, 
												ПараметрыПодключения.ПарольКХранилищу, 
												ИгнорироватьНаличиеПодключеннойБД);
	Исключение
		Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
		Возврат Ложь;
	КонецПопытки;

	Логирователь.ВЛог("Подключение к хранилищу выполнено");

	Возврат Истина;

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры