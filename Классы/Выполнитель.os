#Использовать 1commands
#Использовать "../srс/Модули"

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

&Пластилин
Перем Завершатель; // BSLLS:Typo-off

Функция ВыполнитьКомандныйФайл(СтрокаКоманды, Позиция = 1) Экспорт
		
	стрПозиция = Строка(Позиция);

	Логирователь.ВЛог("Выполняю команду №" + стрПозиция + ": " + СтрокаКоманды);

	КомандныйФайл = Новый КомандныйФайл;
	КомандныйФайл.Создать();
	
	КомандныйФайл.ДобавитьКоманду("@echo off");

	Попытка
		КомандныйФайл.ДобавитьКоманду(Логирователь.LOGOS_CONFIG(Позиция, Истина));
		КомандныйФайл.ДобавитьКоманду(СтрокаКоманды);	
	
		КодВозврата = КомандныйФайл.Исполнить();
		СтрокаВывода = КомандныйФайл.ПолучитьВывод();
	Исключение
		КодВозврата = 1;
		СтрокаВывода = ОписаниеОшибки();
	КонецПопытки;

	СтруктураВозврата = Новый Структура("КодВозврата, Вывод, Позиция", 
											КодВозврата,
											СтрокаВывода,
											Позиция);

	Если КодВозврата <> 0 Тогда
		Логирователь.ВЛог("Результат команды №" + стрПозиция + ": Код возврата " + КодВозврата, Истина);
		Логирователь.ВЛог("Вывод команды №" + стрПозиция + Символы.ПС + СтрокаВывода, Истина);	
	КонецЕсли;
	
	Возврат СтруктураВозврата;

КонецФункции

Процедура СледитьЗаЛогомЗадания(Задание, ПутьКФайлу, Позиция) Экспорт
	
	Выведенные = Новый Массив();

	ПоследняяПопытка = Ложь;

	Пока Истина Цикл
		
		Если Задание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда
			
			Если ПоследняяПопытка Тогда
				Прервать;	
			КонецЕсли;
			
			ПоследняяПопытка = Истина;

		КонецЕсли;

		Логирователь.ВывестиЛогИзФайла(ПутьКФайлу, Выведенные, Позиция);
		
		Если Не ПоследняяПопытка Тогда
			Приостановить(1000);	
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ВыполнитьКомандыМногопоточно(Команды, ПаузаПередСледующим = 5000) Экспорт // BSLLS:Typo-off
	
	Задания = Новый Массив();

	Позиция = 0;
	Для каждого СтрокаКоманды Из Команды Цикл
		
		Позиция = Позиция + 1;

		Если Позиция > 1 Тогда
			Приостановить(ПаузаПередСледующим);	// 5 секунд ожидаем пока запустится предудущее
		КонецЕсли;

		ИмяКласса = "sync_repo"; 
		Если СтрНайти(СтрокаКоманды, ИмяКласса) > 0 Тогда		
			ПутьКФайлуЛога = Логирователь.ПутьКФайлуЛога(ИмяКласса, Позиция, Истина);
			СтрокаКоманды = СтрокаКоманды + " --GroupPosition " + Строка(Позиция);
		Иначе
			ПутьКФайлуЛога = Неопределено;	
		КонецЕсли;

		// Основное задание
		Параметры = Новый Массив(2);
		Параметры[0] = СтрокаКоманды;
		Параметры[1] = Позиция;

		ОсновноеЗадание = ФоновыеЗадания.Выполнить(ЭтотОбъект, "ВыполнитьКомандныйФайл", Параметры);
		Задания.Добавить(ОсновноеЗадание);

		// Следитель
		Если ПутьКФайлуЛога <> Неопределено Тогда

			Параметры = Новый Массив(3);
			Параметры[0] = ОсновноеЗадание;
			Параметры[1] = ПутьКФайлуЛога;
			Параметры[2] = Позиция;

			Следитель = ФоновыеЗадания.Выполнить(ЭтотОбъект, "СледитьЗаЛогомЗадания", Параметры); // BSLLS:Typo-off
			Задания.Добавить(Следитель);
	
		КонецЕсли;

	КонецЦикла;
	
	Попытка
		ФоновыеЗадания.ОжидатьВсе(Задания);
	Исключение
		
		МассивЗаданий = ИнформацияОбОшибке().Параметры;

		Если МассивЗаданий <> Неопределено Тогда

			ШаблонТекстаОшибки = 
				"Ошибки при выполнении команд:
				|%1";

			Для Каждого Задание Из МассивЗаданий Цикл
				
				МассивОшибок = Новый Массив();
				МассивОшибок.Добавить(Задание.ИнформацияОбОшибке.ПодробноеОписаниеОшибки());

			КонецЦикла;

			ВызватьИсключение СтрШаблон(
				ШаблонТекстаОшибки,
				СтрСоединить(МассивОшибок, Символы.ПС));

		КонецЕсли;

	КонецПопытки;

	ПроверитьРезультатВыполнения(Задания);

КонецПроцедуры

Процедура ПроверитьРезультатВыполнения(Задания) // BSLLS:CognitiveComplexity-off
	
	БылиОшибки = Ложь;

	Для каждого Задание Из Задания Цикл
		
		Состояние = Задание.Состояние;
		Если Состояние = СостояниеФоновогоЗадания.Завершено Тогда
			
			Результат = Задание.Результат;	

			Если ТипЗнч(Результат) = Тип("Структура") Тогда
				
				ТекстСообщения = "";
				
				КодВозврата = Результат.КодВозврата;
				Если КодВозврата <> 0 Тогда
					БылиОшибки = Истина;
					ТекстСообщения = "
								|Код возврата команды №" + Результат.Позиция + ": " + Строка(КодВозврата);	
				КонецЕсли;
				
				СтрокаВывода = Результат.Вывод;	
				Если ЗначениеЗаполнено(СтрокаВывода) Тогда
					БылиОшибки = Истина;
					ТекстСообщения = ТекстСообщения + "
										|Вывод команды №" + Результат.Позиция + ": " + Строка(СтрокаВывода);
				КонецЕсли;

				Если ЗначениеЗаполнено(ТекстСообщения) Тогда
					Логирователь.ВЛог(ТекстСообщения);						
				КонецЕсли;
				
			КонецЕсли;
		
		ИначеЕсли Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
			БылиОшибки = Истина;
			Завершатель.ЗавершитьПоОшибке(Задание.ИнформацияОбОшибке.Описание);
		Иначе
			Продолжить;
		КонецЕсли;
				
	КонецЦикла;

	Если БылиОшибки Тогда
		Завершатель.ЗавершитьПоОшибке();
	КонецЕсли;
	
КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта()
    
КонецПроцедуры