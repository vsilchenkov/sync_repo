// ПолучитьВерсиюКонфигурацииИзХранилища
#Использовать "..//../../src/Модули"

&Пластилин
Перем Настройки;

&Пластилин
Перем УправлятельХранилища; // BSLLS:Typo-off
&Пластилин
Перем КомандаОтключитьКонфигураторОтИнформационнойБазы;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Если Не КомандаОтключитьКонфигураторОтИнформационнойБазы.ВыполнитьКоманду() Тогда
		Возврат Ложь;	
	КонецЕсли;

	Хранилище = УправлятельХранилища.Получить();
	
	Логирователь.ВЛог("Получение версии конфигурации из хранилища");

	ФайлыНастроек = Настройки.ФайлыНастроек;
		
	ПутьКФайлу = ФайлыНастроек.ПутьККаталогуCF
								+ "\" 
								+ РаботаСФайлами.СформироватьИмяФайла(Настройки.Идентификатор, "cf");

	Попытка
		Хранилище.ПоследняяВерсияКонфигурацииВФайл(ПутьКФайлу);
	Исключение
		Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
		Возврат Ложь;
	КонецПопытки;
									
	Логирователь.ВЛог("Версия конфигурации из хранилища сохранена");

	Возврат Истина;

КонецФункции

&Асинх
Функция ВыполнитьКомандуАсинх(ПараметрыКоманды) Экспорт // BSLLS:Typo-off
	
	Возврат РаботаСКомандами.ПереопределитьВыполнитьКомандуАсинх(ПараметрыКоманды, ЭтотОбъект);

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры