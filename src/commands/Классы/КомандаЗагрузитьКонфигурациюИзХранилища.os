// ЗагрузитьКонфигурациюИзХранилища
#Использовать "..//../../src/Модули"

&Пластилин
Перем УправлятельКонфигуратора;  // BSLLS:Typo-off
&Пластилин
Перем УправлятельХранилища; // BSLLS:Typo-off

&Пластилин
Перем КомандаОтключитьКонфигураторОтИнформационнойБазы;

&Пластилин
Перем Настройки;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Если Не КомандаОтключитьКонфигураторОтИнформационнойБазы.ВыполнитьКоманду() Тогда
		Возврат Ложь;	
	КонецЕсли;

	Параметры = ПараметрыКоманды.Параметры;
	ИмяРасширения = Параметры.Получить("ИмяРасширения");
	Если Не ЗначениеЗаполнено(ИмяРасширения) Тогда
		ИмяРасширения = "";		
	КонецЕсли;

	Если ЗначениеЗаполнено(ИмяРасширения) Тогда
		ТекстДополнения = ". Расширение - " + ИмяРасширения;
	Иначе
		ТекстДополнения = "";
	КонецЕсли;

	Логирователь.ВЛог("Загрузка конфигурации из хранилища" + ТекстДополнения);

	Если ЗначениеЗаполнено(ИмяРасширения) Тогда
		Возврат ЗагрузитьКонфигурациюИзХранилищаДляРасширения(ИмяРасширения);	
	Иначе
		Возврат ЗагрузитьКонфигурациюИзХранилища();
	КонецЕсли;
	
КонецФункции

Функция ЗагрузитьКонфигурациюИзХранилища()
	
	Конфигуратор = УправлятельКонфигуратора.Получить();
	ПараметрыПодключения = Настройки.ПараметрыПодключения;

	Попытка
		НомерВерсии = 0;
		Конфигуратор.ЗагрузитьКонфигурациюИзХранилища(ПараметрыПодключения.ПутьКХранилищу, 
												ПараметрыПодключения.ЛогинКХранилищу, 
												ПараметрыПодключения.ПарольКХранилищу, 
												НомерВерсии);
	Исключение
		Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
		Возврат Ложь;
	КонецПопытки;

	Возврат Истина;

КонецФункции

Функция ЗагрузитьКонфигурациюИзХранилищаДляРасширения(ИмяРасширения)
	
	Хранилище = УправлятельХранилища.Получить(ИмяРасширения);

	Попытка
		НомерВерсии = "";
		Хранилище.ОбновитьКонфигурациюНаВерсию(НомерВерсии);
	Исключение
		Логирователь.ВЛог(ОписаниеОшибки(), Истина, Истина);
		Возврат Ложь;
	КонецПопытки;

	Возврат Истина;

КонецФункции

&Асинх
Функция ВыполнитьКомандуАсинх(ПараметрыКоманды) Экспорт // BSLLS:Typo-off
	
	Возврат РаботаСКомандами.ПереопределитьВыполнитьКомандуАсинх(ПараметрыКоманды, ЭтотОбъект);

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта()

КонецПроцедуры