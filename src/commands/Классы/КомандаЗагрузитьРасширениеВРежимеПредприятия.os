// ЗагрузитьРасширениеВРежимеПредприятия
#Использовать "..//../../src/Модули"

Перем КомандаЗапуститьВРежимеПредприятия;
Перем Логирователь; // BSLLS:Typo-off

Функция ВыполнитьКоманду(ПараметрыКоманды) Экспорт // BSLLS:UnusedParameters-off
	
	Параметры = ПараметрыКоманды.Параметры;
	
	ИмяРасширения = Параметры.Получить("ИмяРасширения");
	Если Не ЗначениеЗаполнено(ИмяРасширения) Тогда
		Логирователь.ВЛог("Команда: ЗагрузитьРасширениеВРежимеПредприятия - не указано имя расширения", Истина);
		Возврат Ложь;		
	КонецЕсли;

	Путь = Параметры.Получить("Путь");
	Если Не ЗначениеЗаполнено(Путь) Тогда
		Путь = ПутьКФайлуРасширенияПоУмолчанию();	
	КонецЕсли;

	Логирователь.ВЛог("Начинаю загрузку расширения """ + ИмяРасширения + """ в режиме предприятия");

	ОжидатьВыполнения = Истина;		
	КлючиЗапуска = КлючЗапускаОбработки() + " " +  КлючПараметровЗагрузкиРасширения(Путь, ИмяРасширения);
	КомандаЗапуститьВРежимеПредприятия.ЗапуститьВРежимеПредприятия(ОжидатьВыполнения, КлючиЗапуска);
	Возврат Истина;

КонецФункции

Функция КлючЗапускаОбработки()
	
	КлючЗапуска = СтрШаблон("/Execute ""%1\epf\ЗагрузитьРасширениеВРежимеПредприятия.epf""", ТекущийКаталог());
	Возврат КлючЗапуска;

КонецФункции

Функция КлючПараметровЗагрузкиРасширения(Путь, ИмяРасширения)
	
	Разделитель = ";";
	КлючЗапуска = СтрШаблон("Путь=%1%2", Путь, ИмяРасширения);
	КлючЗапуска = КлючЗапуска + Разделитель + "Перезаписывать";	
	КлючЗапуска = КлючЗапуска + ";" + "ЗавершитьРаботуСистемы";
	
	КлючЗапуска = "/C " + """" + КлючЗапуска + """";
	
 	Возврат КлючЗапуска;

КонецФункции

Функция ПутьКФайлуРасширенияПоУмолчанию()
	Возврат "./cfe/";	
КонецФункции

&Асинх
Функция ВыполнитьКомандуАсинх(ПараметрыКоманды) Экспорт // BSLLS:Typo-off
	
	Возврат РаботаСКомандами.ПереопределитьВыполнитьКомандуАсинх(ПараметрыКоманды, ЭтотОбъект);

КонецФункции

&Желудь
&Прозвище("Команда")
Процедура ПриСозданииОбъекта(&Пластилин("КомандаЗапуститьВРежимеПредприятия") _КомандаЗапуститьВРежимеПредприятия,
											&Пластилин("Логирователь") _Логирователь)

	КомандаЗапуститьВРежимеПредприятия = _КомандаЗапуститьВРежимеПредприятия;
	Логирователь = _Логирователь;

КонецПроцедуры