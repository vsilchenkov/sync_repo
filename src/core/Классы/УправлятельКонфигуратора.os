#Использовать v8runner

Перем Конфигуратор;
Перем ВремяОбновленияБазы; // Время обновления базы в минутах

&Пластилин
Перем Настройки;

Функция Получить() Экспорт
	Если Конфигуратор = Неопределено Тогда
		ИнициализироватьКонфигуратор();	
	КонецЕсли;
	Возврат Конфигуратор;	
КонецФункции

Функция ВремяОбновленияБазы() Экспорт
	Возврат ВремяОбновленияБазы; 
КонецФункции

Процедура ВремяОбновленияБазы_Установить(Время) Экспорт
	ВремяОбновленияБазы = Время; 
КонецПроцедуры

Процедура УстановитьПутьКТонкомуКлиенту() Экспорт

	ПараметрыПодключения = Настройки.ПараметрыПодключения;
	ПутьКПлатформе = ПараметрыПодключения.ПутьКПлатформе;

	ПутьКПлатформеТонкий = СтрЗаменить(ПутьКПлатформе, "1cv8.exe", "1cv8c.exe");
	Конфигуратор.ПутьКПлатформе1С(ПутьКПлатформеТонкий);		

КонецПроцедуры

Процедура УстановитьПутьКПлатформе() Экспорт
	ПараметрыПодключения = Настройки.ПараметрыПодключения;
	Конфигуратор.ПутьКПлатформе1С(ПараметрыПодключения.ПутьКПлатформе);		
КонецПроцедуры

Процедура ИнициализироватьКонфигуратор()
	
	ПараметрыПодключения = Настройки.ПараметрыПодключения;

	Конфигуратор = Новый УправлениеКонфигуратором();
	Конфигуратор.ПутьКПлатформе1С(ПараметрыПодключения.ПутьКПлатформе);	
	Конфигуратор.УстановитьКлючРазрешенияЗапуска(ПараметрыПодключения.КодДоступаПриБлокировкиИБ);
	
	ПараметрыСтрокиСоединения = Конфигуратор.ПараметрыСтрокиСоединения();
	ПараметрыСтрокиСоединения.Сервер  = ПараметрыПодключения.СерверБазы;
	ПараметрыСтрокиСоединения.Порт    = ПараметрыПодключения.ПортБазы;
	ПараметрыСтрокиСоединения.ИмяБазы = ПараметрыПодключения.ИмяБазы;

	Конфигуратор.УстановитьКонтекст(ПараметрыСтрокиСоединения, 
										ПараметрыПодключения.ЛогинККонфигуратору, 
										ПараметрыПодключения.ПарольККонфигуратору);

КонецПроцедуры

&Желудь
Процедура ПриСозданииОбъекта()

	ВремяОбновленияБазы = Неопределено;
	Конфигуратор = Неопределено;

КонецПроцедуры