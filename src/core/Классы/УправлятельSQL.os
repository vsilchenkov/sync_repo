#Использовать sql

Перем Соединение;

&Пластилин
Перем Настройки;

&Пластилин
Перем Логирователь; // BSLLS:Typo-off

&Пластилин
Перем Завершатель; // BSLLS:Typo-off

Функция ВыполнитьЗапрос(ТекстЗапроса) Экспорт
	
	Инициализировать();

	Если Не ОткрытьСоединение() Тогда
		Возврат Неопределено;	
	КонецЕсли;	

	Запрос = Новый Запрос();
	Запрос.УстановитьСоединение(Соединение);
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить().Выгрузить();

	Соединение.Закрыть();

	Возврат Результат;

КонецФункции

Функция ВыполнитьКоманду(ТекстКоманды) Экспорт
	
	Инициализировать();

	Если Не ОткрытьСоединение() Тогда
		Возврат Неопределено;	
	КонецЕсли;	

	Запрос = Новый Запрос();
	Запрос.УстановитьСоединение(Соединение);
	Запрос.Текст = ТекстКоманды;
	
	Результат = Запрос.ВыполнитьКоманду();

	Соединение.Закрыть();

	Возврат Результат;

КонецФункции

Функция Получить() Экспорт
	Инициализировать();	
	Возврат Соединение;	
КонецФункции

Процедура Инициализировать()

	Если Соединение <> Неопределено Тогда
		Возврат;	
	КонецЕсли;

	Соединение = Новый Соединение();
	Соединение.ТипСУБД = ТипСУБД(Соединение);
	Соединение.Сервер  = Настройки.СерверSQL;
	Соединение.ИмяБазы = Настройки.ИмяБазыSQL;
	Соединение.ИмяПользователя = Настройки.ИмяПользователяSQL;
	Соединение.Пароль          = Настройки.ПарольSQL;
	
КонецПроцедуры

Функция ОткрытьСоединение()
	
	Соединение.Открыть();

	Если Не Соединение.Открыто Тогда
		Логирователь.ВЛог("Не удалось установить соединение с СУБД", Истина);
		Возврат Ложь;	
	КонецЕсли;

	Возврат Истина;

КонецФункции

Функция ТипСУБД(Соединение)

	ТипСУБД =  Настройки.ТипСУБД;
	Если ТипСУБД = "MSSQLServer" Или Не ЗначениеЗаполнено(ТипСУБД) Тогда
		Возврат Соединение.ТипыСУБД.MSSQLServer;
	Иначе
		Завершатель.ЗавершитьПоОшибке("Не определен тип СУБД");
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры